## UID
UIDs are used to uniquely represent Grobber objects. They are an
essential part of interacting with the API.

Each UID composes all the necessary information to identify an entity.
The most variadic part of an UID is the medium id which is derived from
the title of the medium and sanitised using the following steps:

1. Convert the entire title to lowercase and remove all spaces
2. Find all non-alphanumeric characters (a-z and 0-9) and replace
   them according to the following scheme:
    1. Get the integer Unicode codepoint for this character
    2. Wrap the hexadecimal representation of said integer with underscores

UIDs without a medium source refer to groups. Groups combine all mediums
with the same medium id.


### Format

EBNF:

```
medium type         = "a" | "m" ;

escaped character   = "_" , hexadecimal character , { hexadecimal character } , "_" ;
medium id character = lowercase letter | digit | escaped character ;
medium id           = medium id character , { medium id character } ;

medium source       = lowercase letter , { lowercase letter } ;
language            = "en" | "de" ;

uid                 = medium type , "-" , medium id , [ "-" , medium source ] , "-" , language , [ "_dub" ] ;
```

or simplified:

`medium_type-medium_id(-source?)-language(_dub)?`

And here's a regular expression to parse an UID if you ever come across one.
RegExp:

```
^(a|m)-([a-z0-9_]*)(?:-([a-z]*))?-([a-z]+?)(_dub)?$
```

### Version 1 (Deprecated)
The previous version of the UID format had the following format:

```
medium id character = lowercase letter | digit | "_" ;
medium id           = medium id character , { medium id character } ;

medium source       = lowercase letter , { lowercase letter } ;
language            = "en" | "de" ;

uid                 = medium source , "-" , medium id , "-" , language , [ "_dub" ] ;
```

Grobber still accepts it but you shouldn't use it as the format can't
encode all possible medium ids.
